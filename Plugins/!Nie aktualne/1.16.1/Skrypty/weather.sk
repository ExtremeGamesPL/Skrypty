# Skrypt by wiktoor123
#     wersja: 1.0

options:
    czas: 120 #ustaw co ile sekund może odbyć się głosowanie
    wyswietlanie: procenty #ustaw "procenty" jeżeli wynik ma być widoczny w procentach lub "ilosc" jeżeli ma być wyświetlana tylko ilosc glosów
    infoilosc: tak #ustaw nie, jeżeli po ogłoszeniu wyniku nie ma być widocznej informacji o ilościu głosów (dostepne tylko przy wyświetlaniu procentowym wyników)

on load:
    delete {glosowanie::pogoda::*}
    set {last.glosowanie.pogoda} to unix timestamp of now
    set {wikscript.pogoda.error.name} to ""
    if {wikscript.pogoda.error} is set:
        delete {wikscript.pogoda.error}
    if {@czas} is not integer:
        set {wikscript.pogoda.error} to true
        set {wikscript.pogoda.error.name} to "&4czas"
    if "{@wyswietlanie}" is not "procenty" or "ilosc":
        set {wikscript.pogoda.error} to true
        set {wikscript.pogoda.error.name} to "%{wikscript.pogoda.error.name}% &4wyswietlanie"
    if "{@infoilosc}" is not "tak" or "nie":
        set {wikscript.pogoda.error} to true
        set {wikscript.pogoda.error.name} to "%{wikscript.pogoda.error.name}% &4infoilosc"
    if {wikscript.pogoda.error} is true:
        send "&7» &cSkrypt &9WeatherVote &czostał źle skonfigurowany!" to all players
        send "&7» &cBłędnie skonfigurowane sekcje: %{wikscript.pogoda.error.name}%" to all players
    

command /WikSkrip:pogoda [<text>] [<text>]:
    aliases: pogoda, weathervote
    trigger:
        if arg 1 is "info":
            send "&7» &aInformacje o autorze &9WeatherVote"
            send "&7» &aNick: &ewiktoor123"
            send "&7» &aPortfolio: &e<url:http://skript.wiktoor123.pl>skript.wiktoor123.pl"
        else if {wikscript.pogoda.error} is true:
            send "&7» Skrypt został źle skonfigurowany!"
            send "&7» &4Źle skonfigurowane sekcje: %{wikscript.pogoda.error.name}%"
            stop
        else if {last.glosowanie.pogoda} is smaller than unix timestamp of now:
            if {glosowanie.pogoda} is not set:
                set {glosowanie.pogoda} to true
                delete {glosowanie::pogoda::*}
                send "&7» &7Gracz &e%player%&7 wywoł głosowanie o zmiane pogody!" to all players
                send "                   <tooltip:&eKliknij tu aby zagłosować na słoneczną pogode!>&6<run command:/pogoda glos slonce>[Słońce]<reset>        <tooltip:&eKliknij tu aby zagłosować na deszczową pogode!>&9<run command:/pogoda glos deszcz>[Deszcz]<reset>" to all players
                send "&7» Głosowanie trwa &e15 sekund&7..." to all players
                set {glosowanie::ilpogoda::slonce} to 0
                set {glosowanie::ilpogoda::deszcz} to 0
                set {glosowanie::ilpogoda::all} to 0
                wait 15 seconds
                delete {glosowanie.pogoda}
                send "&7» Głosowanie zakończone!" to all players
                send "&7» &eWyniki:" to all players
                if "{@wyswietlanie}" is "procenty":
                    if {glosowanie::ilpogoda::all} is not 0:
                        set {_slonecznapog} to "%100/{glosowanie::ilpogoda::all}*{glosowanie::ilpogoda::slonce}%&e%%"
                        set {_deszczowapog} to "%100/{glosowanie::ilpogoda::all}*{glosowanie::ilpogoda::deszcz}%&e%%"
                    else:
                        set {_slonecznapog} to "0%%"
                        set {_deszczowapog} to "0%%"
                else:
                    set {_slonecznapog} to "%{glosowanie::ilpogoda::slonce}% &egłosów"
                    set {_deszczowapog} to "%{glosowanie::ilpogoda::deszcz}% &egłosów"
                send "&7»   &6Słońce: &e%{_slonecznapog}%" to all players
                send "&7»   &9Deszcz: &e%{_deszczowapog}%" to all players
                if {glosowanie::ilpogoda::deszcz} is {glosowanie::ilpogoda::slonce}:
                    send "&7» &cRówna ilość głosów nie zmieniono pogody" to all players
                    if "{@infoilosc}" is "tak":
                        if "{@wyswietlanie}" is "procenty":
                            send "&7» &aW głosowaniu wzieło udział %{glosowanie::ilpogoda::all}% &agraczy" to all players
                else:
                    if {glosowanie::ilpogoda::deszcz} is bigger than {glosowanie::ilpogoda::slonce}:
                        send "&7» &aUstawiono &9deszczową &apogode" to all players
                        if "{@infoilosc}" is "tak":
                            if "{@wyswietlanie}" is "procenty":
                                if {glosowanie::ilpogoda::deszcz} is 1:
                                    send "&7» &aMiała 1 głos" to all players
                                else:
                                    send "&7» &aMiała %{glosowanie::ilpogoda::deszcz}% &agłosów" to all players
                        set weather to rain
                    else:
                        send "&7» &aUstawiono &esłoneczną &apogode" to all players
                        if "{@infoilosc}" is "tak":
                            if "{@wyswietlanie}" is "procenty":
                                if {glosowanie::ilpogoda::slonce} is 1:
                                    send "&7» &aMiała 1 głos" to all players
                                else:
                                    send "&7» &aMiała %{glosowanie::ilpogoda::slonce}% &agłosów" to all players
                        set weather to sun
                delete {glosowanie::ilpogoda::*}
                set {last.glosowanie.pogoda} to unix timestamp of now
                add {@czas} to {last.glosowanie.pogoda}
            else:
                if arg 1 is "glos":
                    if arg 2 is "slonce":
                        if {glosowanie::pogoda::%player%} is not set:
                            add 1 to {glosowanie::ilpogoda::slonce}
                            set {glosowanie::pogoda::%player%} to true
                            add 1 to {glosowanie::ilpogoda::all}
                            send "&7» &aZagłosowano na pogodę &esłoneczną&a!"
                        else:
                            send "&7» &cMożesz oddać tylko jeden głos!"
                    else if arg 2 is "deszcz":
                        if {glosowanie::pogoda::%player%} is not set:
                            add 1 to {glosowanie::ilpogoda::deszcz}
                            set {glosowanie::pogoda::%player%} to true
                            add 1 to {glosowanie::ilpogoda::all}
                            send "&7» &aZagłosowano na pogodę &9deszczową&a!"
                        else:
                            send "&7» &cMożesz oddać tylko jeden głos!"
                    else:
                        send "&7» &cGłosowanie dotyczące zmiany pogody już trwa!"
                else:
                    send "&7» &cGłosowanie dotyczące zmiany pogody już trwa!"
        else:
            send "&7» &cGłosowanie o zmiane pogody można wykonywać co &c{@czas} &csekund!"