options:
# ####################################################################################### #
#   ________    ________    ________    ________    ________    ________        ___       #
#  |   __   |  |  ______|  |   __   |  |   __   |  |   __   |  |__    __|      |   |      #
#  |  |__|  |  |  |_____   |  |__|  |  |  |  |  |  |  |__|  |     |  |      ___|   |___   #
#  |       _|  |   _____|  |   _____|  |  |  |  |  |       _|     |  |     |           |  #
#  |  |\  \    |  |        |  |        |  |  |  |  |  |\  \       |  |     |___     ___|  #
#  |  | \  \   |  |_____   |  |        |  |__|  |  |  | \  \      |  |         |   |      #
#  |__|  \__\  |________|  |__|        |________|  |__|  \__\     |__|         |___|      #
#                                                                                         #
# ####################################################################################### #


# ################################### #
        author: matix119
        version: 2.0
# ZAKAZ EDYTOWANIA TYCH INFORMACJI!!! #
# ################################### #


# ############################################# #
# Oficjalna strona skryptu:                     #
#    matix119.000webhostapp.com/skrypty/report/ #
# Ważne zasady użytkowania skryptu:             #
#    matix119.000webhostapp.com/regulamin/      #
# ############################################# #


# ####################################################################################### #
#                                       USTAWIENIA                                        #
#                                                                                         #
#   Ustaw czy ma być wyświetlany prefix przed prawie każdą wiadomością.                   #
        tag: &7[&8Report&7]&r
#                                                                                         #
#   Ustaw czy ustawienie ignorowania wysyłanych zgłoszeń ma być resetowane po wejściu na  #
#   serwer? Jeśli nie, to ustawienie będzie zapamiętywane, choć nie jest zalecane.        #
        reset-ignore-on-join: true
#                                                                                         #
#   Ustaw czas (w sekundach) ograniczający i zapobiegający spamowaniu reportami. Jeśli    #
#   nie chcesz stosować ograniczenia, to wpisz: "0".                                      #
        report-send-cooldown: 15
#                                                                                         #
#   Ustaw sposób wyświetlania powodu w otrzymywanych zgłoszeniach:                        #
#   1 = Powód będzie wyświetlany w osobnej linii pod spodem. Ustawienie odwołuje się do   #
#       opcji REPORT-READ-WITH-REASON.                                                    #
#   2 = Powód będzie wyświetlany w jednej linii. Ustawienie odwołuje się do opcji         #
#       REPORT-READ i REPORT-READ-REASON.                                                 #
        report-reason-style: 1
#                                                                                         #
#   Czy ma być dodatkowo wyświetlana informacja o lokalizacji na mapie zgłoszonego gracza #
#   w otrzymywanych zgłoszeniach i zapisywana także w logach (jeśli są włączone)?         #
#   Ustawienie odwołuje się do opcji REPORT-READ-LOCATION i LOG-FILE-REPORT-LOCATION.     #
        show-cheater-location: true
#                                                                                         #
#   Czy obramowanie (górne i dolne) wiadomości otrzymanego zgłoszenia ma być włączone?    #
#   Ustawienie wyglądu znajdziesz w opcji REPORT-READ-BORDER.                             #
        report-border-enabled: true
#                                                                                         #
#   Ustaw wygląd ważniejszych wiadomości wyświetlanych na czacie:                         #
        report-read: &3%player% &6zgłosił gracza &4%{_p}%
        report-read-reason: &6Powód: &c&o%arg 2%
        report-read-with-reason: &3%player% &6zgłosił gracza &4%{_p}% &6z powodu: &c&o%arg 2%
        report-read-location: &6Koordy: &e&oX: &c&o%{_x}%&6, &e&oY: &c&o%{_y}%&6, &e&oZ: &c&o%{_z}%&6, &e&oŚwiat: &c&o%{_w}%
        report-read-border: &7&l&m=============================================
        msg-send: &2Zgłoszenie zostało wysłane.
        msg-cooldown: &4Odczekaj %{_time}% &4sekund zanim wyślesz kolejne zgłoszenie.
        msg-no-perm: &4Nie masz dostępu do tej komendy.
        msg-err-send-me: &4Nie możesz wysłać zgłoszenia na siebie.
        msg-err-send-other: &4Nie możesz wysłać zgłoszenia na tego gracza.
        msg-err-send-admin: &4Nie możesz zgłaszać cheaterów, ponieważ posiadasz wyższe uprawnienia.
        msg-err-send-offline: &4Nie odnaleziono gracza.
#                                                                                         #
#   Ustaw czy ma być włączone zapisywanie logów zgłaszanych przez graczy do pliku.        #
#   Logi domyślnie będą w lokalizacji: plugins/Skript/logs/report+/zgloszenia.log.        #
#   Ustaw także wygląd tekstu wysyłanych zgłoszeń w pliku.                                #
        logs-enabled: true
        log-file-report: %player% zgłosił gracza %{_p}%.
        log-file-report-with-reason: %player% zgłosił gracza %{_p}% z powodu: %arg 2%
        log-file-report-location: (Koordy: X: %{_x}%, Y: %{_y}%, Z: %{_z}%, Świat: %{_w}%)
#                                                                                         #
# ####################################################################################### #


command /report [<offlineplayer>] [<text>]:
    aliases: /zglos, /zgłoś
    trigger:
        if player has permission "report.send" or "report.*" or "*":
            if player don't have permission "report.read" or "report.*" or "*":
                if arg 1 is set:
                    set {_p} to arg 1
                    if {_p} is player:
                        send "{@msg-err-send-me}" to player
                    else:
                        if {_p} is online:
                            if {_p} don't have permission "report.read" or "report.*" or "*":
                                if {rcooldown.%player%} > 0:
                                    set {_time} to {rcooldown.%player%}
                                    send "{@msg-cooldown}" to player
                                    stop
                                send "{@msg-send}" to player
                                loop all players:
                                    if loop-player has permission "report.read" or "report.*" or "*":
                                        if {rignore.%loop-player%} is not set:
                                            if {@report-border-enabled} is true:
                                                send "{@report-read-border}" to loop-player
                                            if arg 2 is set:
                                                if {@report-reason-style} is 2:
                                                    send "{@tag} {@report-read-with-reason}" to loop-player
                                                if {@report-reason-style} is 1:
                                                    send "{@tag} {@report-read}" to loop-player
                                                    send "{@tag} {@report-read-reason}" to loop-player
                                            if arg 2 is not set:
                                                send "{@tag} {@report-read}" to loop-player
                                            if {@show-cheater-location} is true:
                                                set {_x} to round down x-location of {_p}
                                                set {_y} to round down y-location of {_p}
                                                set {_z} to round down z-location of {_p}
                                                set {_w} to {_p}'s world
                                                send "{@tag} {@report-read-location}" to loop-player
                                                set {_cheatloc} to " {@log-file-report-location}"
                                            if {@report-border-enabled} is true:
                                                send "{@report-read-border}" to loop-player
                                if {@logs-enabled} is true:
                                    if {_cheatloc} is not set:
                                        set {_cheatloc} to ""
                                    if arg 2 is set:
                                        log "{@log-file-report-with-reason}%{_cheatloc}%" to "report+/zgloszenia.log"
                                    else:
                                        log "{@log-file-report}%{_cheatloc}%" to "report+/zgloszenia.log"
                                if {@report-send-cooldown} > 0:
                                    if {rcooldown.%player%} is not set:
                                        set {rcooldown.%player%} to {@report-send-cooldown}
                                        loop {@report-send-cooldown} times:
                                            wait 1 second
                                            remove 1 from {rcooldown.%player%}
                                        delete {rcooldown.%player%}
                            else:
                                send "{@msg-err-send-other}" to player
                        else:
                            send "{@msg-err-send-offline}" to player
                else:
                    send "Zgłasza gracza cheatera i wysyła zgłoszenie do administracji." to player
                    send "/report <gracz> [powód]" to player
            else:
                send "{@msg-err-send-admin}" to player
        else:
            send "{@msg-no-perm}" to player

command /report-ignore:
    trigger:
        if player has permission "report.read" or "report.*" or "*":
            if player has permission "report.ignore" or "report.*" or "*":
                if {rignore.%player%} is not set:
                    set {rignore.%player%} to true
                    send "{@tag} &6Wysyłane zgłoszenia od graczy będą &cignorowane&6." to player
                    stop
                if {rignore.%player%} is set:
                    delete {rignore.%player%}
                    send "{@tag} &6Wysyłane zgłoszenia od graczy będą &apokazywane&6." to player
                    stop
            else:
                send "{@msg-no-perm}" to player
        else:
            send "{@msg-no-perm}" to player

command /reportplus [<text>]:
    aliases: /report+
    trigger:
        if player has permission "report.admin" or "report.*" or "*":
            if arg 1 is "help" or "pomoc" or "?":
                send "&7&l&m==================&r&7&l[&8&lReport+&7&l]&7&l&m==================" to player
                send "&4/report <gracz> [powód] &e- &eWysyła zgłoszenie (tylko gracz)." to player
                send "&4/report-ignore &e- &eIgnoruje wysyłane zgłoszenia." to player
                send "&4/reportplus reload &e- &eRestartuje skrypt." to player
                send "&4/reportplus info &e- &eInformacje o skrypcie." to player
                send "&7&l&m============================================" to player
                stop
            if arg 1 is "reload" or "restart" or "r":
                set {_t1} to now
                execute console command "/skript reload %script%.sk"
                set {_t2} to now
                set {_t} to "%difference between {_t1} and {_t2}%"
                replace " seconds" and " second" with "s" in {_t}
                send "{@tag} &6Zrestartowano skrypt (&6czas: %{_t}%&6). Sprawdź konsolę czy nie ma błędów." to player
                stop
            if arg 1 is "info" or "informacje":
                send "&7&l&m==================&r&7&l[&8&lReport+&7&l]&7&l&m==================" to player
                send "&e» &6Skrypt napisany przez: &c{@author}" to player
                send "&e» &6Wersja: &c{@version}" to player
                send "&e» &6Informacje: &cmatix119.000webhostapp.com/skrypty/report/" to player
                send "&4Dziękuję za używanie skryptu Report+ :)." to player
                send "&7&l&m============================================" to player
                stop
        else:
            send "{@msg-no-perm}" to player

on join:
    if {@reset-ignore-on-join} is true:
        if {rignore.%player%} is set:
            delete {rignore.%player%}
    if {rcooldown.%player%} is set:
        delete {rcooldown.%player%}
			
on skript start:
    send "[Skript] Skrypt Report+ v{@version} zostal uruchomiony!" to console

on skript stop:
    send "[Skript] Skrypt Report+ v{@version} zostal wylaczony!" to console